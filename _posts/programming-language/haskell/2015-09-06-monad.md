---
title: Monad(单子)
author: He Tao
date: 2015-09-06
tag: [Haskell]
category: 编程语言
layout: post
---

> A monad is just a monoid in the category of endofunctors

Monad, 单子, 是自函子范畴上的一个幺半群，是Haskell中一个及其重要的概念。

<!--more-->

Monad
------

Functor 是一个抽象概念，代表是一种可以被 map over 的值。Applicative Functor，代表一种带有 context 的型态，我们可以用函数操作他而且同时还保有他的 context。 而对于一个Monad，可以看作是支持 `>>=` 操作的 applicative functors，`>>=` 称为 `bind`。所谓 `bind`, 即把一个具有 context 的值 m a，传给一个只接受普通值 a 为参数的函数，并回传一个具有 context 的值的函数，并得到一个Monad。 `bind` 函数的表述如下：

    (>>=) :: (Monad m) => m a -> (a -> m b) -> m b

> The Monad class defines the basic operations over a monad, a concept from a branch of mathematics known as category theory.

> From the perspective of a Haskell programmer, however, it is best to think of a monad as an abstract datatype of actions. Haskell's do expressions provide a convenient syntax for writing monadic expressions.

最小化的完整定义：

~~~haskell
class Monad m where
    return :: a -> m a
    (>>=) :: m a -> (a -> m b) -> m b
~~~

`return`和`(>>=)`的定义应该满足 Monad Laws:

+ Left identity

        return a >>= k  ==  k a

+ Right identity

        m >>= return  ==  m

+ **Associativity**

        m >>= (\x -> k x >>= h)  ==  (m >>= k) >>= h

此外，还要满足如下需求：

    fmap f xs  ==  xs >>= return . f

在 Monad typeclass 中定义的第一个函数是 return。它其实等价于 Applicative 的 pure 函数。他的型态是 (Monad m) => a -> m a。他接受一个普通值并把他放进一个最小的 context 中。也就是说，将一个值转换为一个 Action。而 `>>=`函数则是将一个 Monad 作为参数传给一个以值为参数，以 Monad 为返回值的函数，并最终得到一个 Monad。

`Maybe`类型是 Monad 的一个实例类型，定义为：

~~~haskell
instance  Monad Maybe  where
    (Just x) >>= k      = k x
    Nothing  >>= _      = Nothing

    return              = Just
    fail _              = Nothing
~~~

Haskell中的List作为Monad的定义如下：

~~~
instance Monad [] where
    return x = [x]
    xs >>= f = concat (map f xs)
    fail _ = []
~~~

`do` notation
-------------

`do` notation 用来来串连所有的 monadic value。在 do expression 中，每一行都是一个 monadic value。要检查处理的结果的话，就要使用 <-。例如如果我们拿到一个 Maybe String，并用 <- 来绑定给一个变量，那个变量就会是一个 String。

由于 do 表示法是一行一行写，他们会看起来很像是命令式的写法。**但实际上他们只是代表串行而已，每一步的值都倚赖前一步的结果，并带着他们的 context 继续下去。**

可以说，Monad也可以被称为"computation builder"，monad 将很多条有关联的语句组合到一起，并对外体现出纯函数(pure functional)的风格和接口定义。**同时，也并不是所有的Monad都有副作用。Monad IO 有副作用，而 Maybe, State Moand 等 Monad 是没有副作用的。**

> Both the listcomprehension syntax and the do-notation are syntactic sugar for chaining operations using the `>>=` operator. A monad is basically just a type that supports the `>>=` operator.

例如，

    [x*2 | x<- [1..10], odd x]

可以写成是：

    do
        x <- [1..10]
        if odd x
            then [x * 2]
            else []

甚至是：

    [1..10] >>= (\x -> if odd x then [x*2] else [])

从定义上看，Monad就是两个接口，一个`return`，一个`bind`，只要实现了这两种操作的类型，都是Monad。

Monad and Category Theory
-------------------------

在范畴论中，函子（functor）是范畴间的一类映射，通俗地说，是范畴间的同态。
单子(Moand)是自函子的Monoidal范畴上的一个幺半群，该Monoidal范畴的张量积是自函子的复合(composition)，单位元是Id functor。

> All told, a monad in X is just a monoid in the category of endofunctors of X, with product × replaced by composition of endofunctors and unit set by the identity endofunctor.

-- Saunders Mac Lane, _Categories for the Working Mathematician_

Solve problems
---------------

Monads are typically used to solve problems like:

+ I need to make new capabilities for this type and still combine old functions on this type to
use the new capabilities.
+ I need to capture a bunch of operations on types and represent those operations as
composable objects, building up larger and larger compositions until I have just the right
series of operations represented, and then I need to start getting results out of the thing.
+ I need to represent side-effecting operations cleanly in a language that hates side effects.

Moandic functions
-----------------

> A monad is an "amplifier" of types that obeys certain rules and which has certain operations provided.

+ `liftM`

`liftM` 函数接受一个函数跟一个 monadic value，然后把函数 map over 那些 monadic value，最终得到一个 monadic value 作为结果。某种程度上说， `liftM` 函数与 `fmap` 函数具有相似之处。

~~~haskell
-- | Promote a function to a monad.
liftM   :: (Monad m) => (a -> b) -> m a -> m b
liftM f mval              = do { aval <- mval; return (f aval) }
~~~

此外，还有适用于不同参数个数的 LiftM2, LiftM3, LiftM4 和 LiftM5 的定义。

+ `join`

如果一个 monadic value 的结果是另一个 monadic value，也就是其中一个 monadic value 被包在另一个里面，即嵌套 Monad 。此时，就需要用到`join`函数来将其变为一个普通的 monadic value。函数的定义与实现：

~~~haskell
-- | The 'join' function is the conventional monad join operator. It is used to
-- remove one level of monadic structure, projecting its bound argument into the
-- outer level.
join              :: (Monad m) => m (m a) -> m a
join x            =  x >>= id
~~~

+ `filterM`

filter 函数是 Haskell 中不可或缺的要素。他接受一个断言(predicate)跟一个 list 来过滤掉断言为否的部份并回传一个新的 list。predicate 能接 list 中的一个元素并回传一个 Bool 型态的值。但如果 Bool 型态其实是一个 monadic value ，也就是有一个 context 呢？也就是说，最终的谓词的真或者假依赖于上下文。此时，我们就需要用到 `filterM`函数。

~~~haskell
-- | This generalizes the list-based 'filter' function.

filterM          :: (Monad m) => (a -> m Bool) -> [a] -> m [a]
filterM _ []     =  return []
filterM p (x:xs) =  do
   flg <- p x
   ys  <- filterM p xs
   return (if flg then x:ys else ys)
~~~

+ `(=<<)`

`(=<<)` 函数与`(>>=)` 函数类似，只不过是交换了两个参数的位置。

~~~haskell
-- | Same as '>>=', but with the arguments interchanged.
{-# SPECIALISE (=<<) :: (a -> [b]) -> [a] -> [b] #-}
(=<<)           :: Monad m => (a -> m b) -> m a -> m b
f =<< x         = x >>= f
~~~

+ `(>=>)` 和 `(<=<)`

Kleisli composition of monads, 用于组合两个返回值为`Monad`的函数：

~~~haskell
-- | Left-to-right Kleisli composition of monads.
(>=>)       :: Monad m => (a -> m b) -> (b -> m c) -> (a -> m c)
f >=> g     = \x -> f x >>= g

-- | Right-to-left Kleisli composition of monads. @('>=>')@, with the arguments flipped
(<=<)       :: Monad m => (b -> m c) -> (a -> m b) -> (a -> m c)
(<=<)       = flip (>=>)
~~~

其中，`(<=<)`函数与函数合成函数 `.`类似(`f . g x = f(g(x))`)。

+ `ap`

~~~haskell
ap :: Monad m => m (a -> b) -> m a -> m b
~~~

`ap` applies a function inside a monad to a value inside a monad of the same type. In many situations, the `liftM` operations can be replaced by uses of ap, which promotes function application.

    return f `ap` x1 `ap` ... `ap` xn

is equivalent to

    liftMn f x1 x2 ... xn

Functor, Applicative 和 Monad
-----------------------------

Functor, Applicative 和 Monad 本质上都代表着 Haskell 对于 non-deterministic 的情形的处理，以及如何将这些内容与 deterministic 的代码，也就是Haskell中的纯函数，建立起联系。

与之对应，`fmap` 将普通函数作用在函子上，得到包含结果的函子，我们注意到 `fmap`函数的类型定义：

    fmap :: (Functor f) => (a -> b) -> f a -> f b

Applicative Funtor中的`<*>`更进一步，将一个`f (a -> b)` 应用到一个functor上，然后返回包含结果的functor：

    <*> :: (Functor f) => f (a -> b) -> f a -> f b

Monad 中的 `>>=` 将一个以值为参数，monad为返回值的函数应用到对应类型的monad上，并得到包含结果的monad。

函子具有恒值映射，`fmap` 的类型至签名也可以写成：

    fmap :: (Functor f) => (a -> b) -> (f a -> f b)

有`a -> b`函数，在列表上就有`[a] -> [b]` ，在树上就有`Tree a -> Tree a`。 有 `[a] -> [b]`，在列表上就有`[[a]] -> [[b]]`，依次类推。

函子发现之后，Philip Wadler跳过了Applicative发现了Monad，Applicative Functor看作是计算时有副作用的Continuation。

Monad在计算的时候，后一个计算问题可以用到前面的参数，也就是说各个计算之间不是互相独立的，而是有依赖关系的，`do`语句块是串行执行的。例如：

~~~haskell
main :: IO ()
main = do
    s <- getLine
    t <- getLine
    putStrLn $ s ++ t
~~~

对这段代码desugar之后的结果(GHC编译器时使用参数 `-ddump-ds`)呢？是这样的：

~~~haskell
main :: IO ()
main = getLine >>= (\s -> getLine >>= (\t ->putStrLn (s ++ t)))
~~~

Monad通过`>>=`把各种计算串连起来，而对于中间结果的使用没有做任何限定，使用起来十分自由、也相当容易理解，因为大部分的计算都满足这种特性, STM、IO、Parser、Iteratee、State、Writer、Reader 等等。

后来人们发现了比Monad更为一般的Applcative，Applicative是同一个范畴上的映射，其中的`pure`就是Monad的`return`(从一个值构造包含该值的函子)，`f (a -> b) -> f a -> f b`意思是说，即便我们这个容器`f`里是一个函数，我们还是可以将其应用到`f a`类型上，**计算`a`类型的值与应用这个函数两个过程是分离的**。

在Monad之后，Applicative的出现正是由于抽象的需要。程序的抽象的粒度应当适当地小，这样更有利于模块化，可复用和适应变化。Haskell的逻辑是自底向上，而不是All-in-One。正如Haskell中有了Ord还需要Eq一样，对于抽象是要分层的。在工程实践中，例如Java的Collection的设计，OSI七层网络模型的设计等都是这个道理。
有些时候我们不需要Monad，不需要把计算的各个结果在计算过程中混杂到一起，维护Context是需要成本的，这样也更有利于优化和抽象，同时，Applicative无上下文的依赖关系，也更容易做并发和并行。更进一步，Applicative和Monad都是自函子范畴上的一个幺半群，不同的是Applicative是自函子的水平方向的组合，Monad是自函子的垂直方向的组合。

`MonadPlus`
----------

`MonadPlus`: Monads that also support choice and failure.

~~~haskell
class (Alternative m, Monad m) => MonadPlus m where
~~~

Methods in `MonadPlus` typeclass:

+ `mzero`

    the identity of mplus. It should also satisfy the equations.

~~~haskell
mzero :: m a
mzero >>= f  =  mzero
v >> mzero   =  mzero
~~~

+ `mplus`

    an associative operation

~~~haskell
mplus :: m a -> m a -> m a
mplus = (<|>)
~~~

参考
---

1. [All About Monad][1]
2. [What a Monad is not][2]
3. [Monads for functional programming][4], _Philip Wadler_

<!--links-->

[1]: https://wiki.haskell.org/All_About_Monads
[2]: https://wiki.haskell.org/What_a_Monad_is_not
[3]: https://www.cs.rit.edu/~swm/cs561/All_About_Monads.pdf
[4]: http://homepages.inf.ed.ac.uk/wadler/papers/marktoberdorf/baastad.pdf
