---
title: STM (软件事务内存)
author: He Tao
date: 2015-08-23
tag: [并发模型]
category: 并发模型
layout: post
---

计算机科学中，Software　Transactional Memory(STM，软件事务内存)是一种与数据库的事务类似的并发控制结构，用于控制并发计算中对共享内存的访问，是基于锁的同步的一种替代机制。STM在软件层面实现，而不是基于硬件部件。当一段代码需要读或者写共享的内存区域时，就产生了一次事务。
1986年，Tom Knight 提出了这一理论，Nir Shavit 和 Dan Touitou 在 1995 年发表的论文 [Software Transactional Memory][5] 使得STM开始流行起来，越来越多地收到人们的关注。

<!--more-->

STM
----

不同于基于锁的多线程并发控制技术，STM技术中，一个线程在修改共享内存时，不用考虑其他线程当时的行为，并且，每次对共享内存的修改都被记入日志。
在锁机制中，由写者(writer)来保证在修改共享内存时不对其他的线程产生不利的影响，而在STM中，这一责任落在了读者(reader)上面。
完成一次事务后，reader会验证没有其他线程正在对共享内存进行并发修改，如果验证成功，将会产生一次提交(commit)，将结果写入共享内存，made permanent。
否则，这次事务便会终止(abort)，撤销在这期间的所有更改，然后这次事务会再次从头开始执行(retry, re-execute)直到取得成功。

STM的好处在于提高了程序的并发度，每个线程都不用等待对一个资源的访问，不同的线程可以同时安全地修改共享内存中不相交的部分(类似于并发地修改数据库中的不同记录)。
然而，在实践中，在小数量(1-4，取决于应用程序)的处理器上运行时，与调度良好的锁机制相比，STM遇到了性能问题，这种要是由于维护日志导致和花费在提交的时间上导致的。
但在最坏情形下，根据Simon Peyton-Jones的演讲[Programming in the Age of Concurrency: Software Transactional Memory][7]，性能损失也不会超过两倍。理论上，n 个并发事务(transactions)的所需要的时间和空间都为O(n)。

除了性能上的优势，STM也使得多线程程序变得更加容易编写和维护。Lock-based programming 存在很多众所周知的问题(引自[Wikipedia][8])：

+ Locking requires thinking about overlapping operations and partial operations in distantly separated and seemingly unrelated sections of code, a task which is very difficult and error-prone.
+ Locking requires programmers to adopt a locking policy to prevent deadlock, livelock, and other failures to make progress. Such policies are often informally enforced and fallible, and when these issues arise they are insidiously difficult to reproduce and debug.
+ Locking can lead to priority inversion, a phenomenon where a high-priority thread is forced to wait for a low-priority thread holding exclusive access to a resource that it needs.

而在STM中，这些问题都变得很容易解决，每一个事务都可以看成是一个独立的线程，编程程序的难度会因此减小。但是，需要终止失败的事务也限制了事务的行为，这些事务不能有不可撤销的行为，例如IO。在实践中，这些问题可以通过创建buffer，用于queue up程序中的irreversible operations来解决。在Haskell中，类型系统用来保证事务满足这一限制(permits only side effects on TVars)。

多个原子性的事务可以组合成一个更大的原子事务，而在lock-based programming中，这是不可能的。

__Language Support for Lightweight Transactions__ 和 __Proposed the Idea of Using th Classical Conditional Critical Region(CCR) to Represent Transactions__ 两篇论文提出了原子块(atomic block)的方式来描述事务，例如：

    // Insert a node into a doubly linked list atomically
    atomic {
        newNode->prev = node;
        newNode->next = node->next;
        node->next->prev = newNode;
        node->next = newNode;
    }

When the end of the block is reached, the transaction is committed if possible, or else aborted and retried.

CCR(Conditional Critical Region)也允许guard condtion:

    atomic (queueSize > 0) {
        remove item from queue and use it
    }

当条件不满足时，还可以一直重试事务，知道其他的事物产生一次commit并影响到当前事务的条件：

    atomic {
        if (queueSize > 0) {
            remove item from queue and use it
        } else {
            retry
        }
    }

**This ability to retry dynamically late in the transaction simplifies the programming model and opens up new possibilities.**

当事务出现冲突时，我们需要采取一定的措施来依次提交事务，这需要用到Commitment Ordering，在这方面，已经有很多理论和算法方面的研究。

事务性的保证
----------

我们知道，在数据中，事务性必须满足ACID(Atomicity, Consistency, Isolation, Durability)，而软件事务内存又是怎样体现这些事务性的要求的呢？

+ 原子性(atomicity)

在其他的事务看来，当前事务的作用要不都发生，要不都不发生。

+ 隔离性(ioslation)

多个事务可以同时运行，但多个事务同时运行的结果应该与串行这些事务的结果相一致。

+ 一致性(consistency)

事务的一系列校验中任何一个失败，那么所有的修改都不发生。

+ 持久性(durability)

STM的实现
--------

STM在多种编程语言中都有实现。
Haskell中，有基于论文[Composable Memory Transactions][9]实现的`Control.Concurrent.STM`以及对应的Monad：`Control.Monad.STM`。
University of Cambridge 的研究人员基于论文 [Language Support for Lightweight Transactions][3] 以及其他的理论研究，使用 C 语言实现了一套 lock-free 的数据结构 [Pratical lock-free data structures][2]。
Python的JIT解释器pypy也通过STM机制克服Python的 Global interpreter lock(GIL，全局解释器锁)带来的问题，实现了[pypy-stm][4]以更加充分地利用CPU资源，提高程序的运行效率。
在JVM上，Clojure语言通过标识和状态分离机制实现了STM，以便于更好地服务于并发程序。而 Multiverse和源自于Scala的Akka框架的成功也将STM带入了Java语言。Akka使用了提交屏障(CommitBarrier，基于Java的倒计时锁存器建立)这一概念来使不同线程上的多个事务能够像一个单一的原子操作块般被管理，在跨越多个线程的所有事务之间，设置一个单一的、共同的屏障点，一旦屏障被触及，所有的事务均将自动提交。

而在硬件方面，Inter Haswell 架构的CPU已经开始通过Transactional Synchronization Extensions (TSX)机制对Tansactional Synchronization 提供硬件级别的[支持][6]，以使得能够在软件层次更好、更方便、更高效地实现事务性内存访问和同步。

STM使用示例(Dining philosophers problem)
--------------------------------------

接下来，我们将通过使用STM来解决[Dining Philosopher Problem][11]来演示STM的使用。

首先，定义一个binary semaphores来表示对筷子的占用(锁)，实现相应的P、V操作：

```haskell
type Semaphore = TVar Bool

newSem :: Bool -> IO Semaphore
newSem val = newTVarIO val

p :: Semaphore -> STM ()
p sem = do
    val <- readTVar sem
    if val
        then writeTVar sem False
        else retry

v :: Semaphore -> STM ()
v sem = writeTVar sem True
```

`retry` 的语义是放弃之前的结果，并从事务的最开始的地方重新开始。

接下来，定义一个缓冲区：

```haskell
type Buffer a = TVar [a]

newBuffer :: IO (Buffer a)
newBuffer = newTVarIO []

put :: Buffer a -> a -> STM ()
put buffer item = do
    xs <- readTVar buffer
    writeTVar buffer (xs ++ [item])

get :: Buffer a -> STM a
get buffer = do
    xs <- readTVar buffer
    case xs of
        [] -> retry
        (item:rest) -> do
            writeTVar buffer rest
            return item
```

然后定义哲学家的行为：

```haskell
philosopher :: Int -> Semaphore -> Semaphore -> IO ()
philosopher n left right = do
    -- thinking.
    putStrLn ("Philosopher " ++ show n ++ " is thinking.")
    randomDelay

    atomically $ do
        p left
        p right

    -- eating.
    putStrLn ("Philosopher " ++ show n ++ " is eating.")
    randomDelay

    atomically $ do
        v left
        v right

    -- continue.
    philosopher n left right

    where randomDelay = randomRIO (100000,500000) >>= threadDelay
```

最后，创建 n 支筷子，开始运行程序：

```haskell
simulate n = do
    forks <- replicateM n (newSem True)
    mapM_ (\i -> forkIO (philosopher i (forks !! i) (forks !! ((i+1) `mod` n)))) [0..n-1]
```

我们会发现输出显得很乱，原因就在于多个线程同时进行IO输出，为了解决这个问你，我们需要使用STM创建一个IO Buffer：

```haskell
newBuffer :: IO (Buffer a)
newBuffer = newTVarIO []

put :: Buffer a -> a -> STM ()
put buffer item = do
    xs <- readTVar buffer
    writeTVar buffer (xs ++ [item])

get :: Buffer a -> STM a
get buffer = do
    xs <- readTVar buffer
    case xs of
        [] -> retry
        (item:rest) -> do
            writeTVar buffer rest
            return item

output buffer = do
    (atomically $ get buffer) >>= putStrLn
    output buffer
```

在哲学家的代码中，将直接操作 IO 的 语句改为将IO操作参数加入到buffer中：

    -- use atomically
    atomically $ put out ("Philosopher " ++ show n ++ " is thinking.")

相对应地，`simulate`函数的代码改为：

```haskell
simulate n = do
    forks <- replicateM n (newSem True)
    outputBuffer <- newBuffer
    mapM_ (\i -> forkIO (philosopher i outputBuffer (forks !!  i) (forks !! ((i+1) `mod` n)))) [0..n-1]
    output outputBuffer
```

运行`simulate 4`，便能得到模拟4个哲学家进餐问题的输出结果。我们知道，使用锁来解决哲学家进餐问题时，我们需要约定一个全局一致的获取锁和释放锁的顺序，否则会发生死锁的问题，而当我们使用STM时，可以保证内存访问的原子性和一致性，这一问题也就迎刃而解。

完整程序：[Philosopher.hs][12]

参考
---

1. [Software Transactional Memory (STM)][1]
2. [Chapter 10. Software Transactional Memory][10]


<!-----------------------------------------links-------------------------->

[1]: http://www.javacodegeeks.com/2013/01/software-transactional-memory-stm.html
[2]: http://www.cl.cam.ac.uk/research/srg/netos/projects/archive/lock-free/
[3]: http://www.cl.cam.ac.uk/research/srg/netos/papers/2003-ls4lt.pdf
[4]: http://pypy.readthedocs.org/en/latest/stm.html
[5]: http://dl.acm.org/citation.cfm?id=224987
[6]: https://software.intel.com/en-us/blogs/2012/02/07/transactional-synchronization-in-haswell
[7]: http://channel9.msdn.com/ShowPost.aspx?PostID=231495
[8]: https://en.wikipedia.org/wiki/Software_transactional_memory
[9]: http://research.microsoft.com/en-us/um/people/simonpj/papers/stm/stm.pdf
[10]: http://chimera.labs.oreilly.com/books/1230000000929/ch10.html
[11]: https://en.wikipedia.org/wiki/Dining_philosophers_problem
[12]: {{site.url}}/sourcecode/Philosopher.hs
